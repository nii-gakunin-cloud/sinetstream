/*
 * Copyright (C) 2020 National Institute of Informatics
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'application'
}

sourceCompatibility = 1.11

version = '1.9.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-cli:commons-cli:1.7.0'
    implementation 'net.sinetstream:SINETStream-api:1.+'
    implementation 'net.sinetstream:SINETStream-kafka:1.+'
    implementation 'net.sinetstream:SINETStream-mqtt:1.+'
    implementation 'net.sinetstream:SINETStream-type-image:1.+'
    implementation group: 'org.bytedeco', name: 'javacv-platform', version: '1.5.10'
    runtimeOnly 'org.slf4j:slf4j-nop:2.0.13'
}

application {
    mainClassName = 'jp.ad.sinet.stream.example.image.ImageProducer'
    applicationName = 'image-producer'

    // WORKAROUND for:
    //     WARNING: An illegal reflective access operation has occurred
    //     WARNING: Illegal reflective access by org.eclipse.paho.client.mqttv3.internal.FileLock
    // see https://github.com/eclipse/paho.mqtt.java/issues/507#issuecomment-814681499
    applicationDefaultJvmArgs = ['--add-opens=java.base/sun.nio.ch=ALL-UNNAMED']
}

task createExtraRunApp(type: CreateStartScripts) {
    mainClassName = 'jp.ad.sinet.stream.example.image.ImageConsumer'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'image-consumer'

    // WORKAROUND for:
    //     WARNING: An illegal reflective access operation has occurred
    //     WARNING: Illegal reflective access by org.eclipse.paho.client.mqttv3.internal.FileLock
    // see https://github.com/eclipse/paho.mqtt.java/issues/507#issuecomment-814681499
    defaultJvmOpts = ['--add-opens=java.base/sun.nio.ch=ALL-UNNAMED']
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createExtraRunApp)
    fileMode = 0755
}
